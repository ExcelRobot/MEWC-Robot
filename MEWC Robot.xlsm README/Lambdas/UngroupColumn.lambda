UngroupColumn =LAMBDA(data,[column_index],[delimiter], LET(
   \\LambdaName, "UngroupColumn",
   _ColIndex, IF(
      OR(ISOMITTED(column_index), ISBLANK(column_index), column_index = ""),
      COLUMNS(data),
      column_index
   ),
   _Delim, IF(
      OR(ISOMITTED(delimiter), ISBLANK(delimiter), delimiter = ""),
      ",",
      delimiter
   ),
   _Rows, ROWS(data),
   _Cols, COLUMNS(data),
   _Keys, IF(_ColIndex = _Cols, DROP(data, , -1), RemoveCols(data, _ColIndex)),
   _Values, CHOOSECOLS(data, _ColIndex),
   _Result, REDUCE(
      "",
      SEQUENCE(_Rows),
      LAMBDA(acc,r, LET(
         key, CHOOSEROWS(_Keys, r),
         valueList, TEXTSPLIT(INDEX(_Values, r, 1), , _Delim),
         trimmed, TRIM(valueList),
         keyCols, MAKEARRAY(
            ROWS(trimmed),
            COLUMNS(key),
            LAMBDA(row,col, INDEX(key, 1, col))
         ),
         pairs, IF(
            _ColIndex = _Cols,
            HSTACK(keyCols, trimmed),
            InsertCols(keyCols, trimmed, _ColIndex)
         ),
         IF(@acc = "", pairs, VSTACK(acc, pairs))
      ))
   ),
   _Result
));