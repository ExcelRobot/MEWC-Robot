UngroupColumn = LAMBDA(data,[column_index],[delimiter], LET(
   \\LambdaName, "UngroupColumn",
   _ColIndex, IF(
      OR(ISOMITTED(column_index), ISBLANK(column_index), column_index = ""),
      COLUMNS(data),
      column_index
   ),
   _Delim, IF(OR(ISOMITTED(delimiter), ISBLANK(delimiter)), ",", delimiter),
   _Rows, ROWS(data),
   _Cols, COLUMNS(data),
   _Keys, IF(_ColIndex = _Cols, DROP(data, , -1), RemoveCols(data, _ColIndex)),
   _Values, CHOOSECOLS(data, _ColIndex),
   _Result, REDUCE(
      "",
      SEQUENCE(_Rows),
      LAMBDA(acc,r, LET(
         key, CHOOSEROWS(_Keys, r),
         vals, INDEX(_Values, r, 1),
         valueList, IF(_Delim = "", MID(vals, SEQUENCE(LEN(vals)), 1), TEXTSPLIT(vals, , _Delim)),
         trimmed, TRIM(valueList),
         keyCols, MAKEARRAY(ROWS(trimmed), COLUMNS(key), LAMBDA(row,col, INDEX(key, 1, col))),
         allCols, IF(
            _ColIndex = _Cols,
            HSTACK(keyCols, trimmed),
            InsertCols(keyCols, trimmed, _ColIndex)
         ),
         IF(@acc = "", allCols, VSTACK(acc, allCols))
      ))
   ),
   _Result
));